plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'idea'
    id 'maven-publish'
    id 'java'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'org.ktypeparser'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.7")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7")

    implementation("org.overviewproject:mime-types:0.1.3")
    
    testImplementation("org.junit.jupiter:junit-jupiter:5.6.2")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

def pomConfig = {
    licenses {
        license {
            name "GNU GENERAL PUBLIC LICENSE, Version 3.0"
            url "https://www.gnu.org/licenses/gpl-3.0.en.html"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "alekseinovikov"
            name "Aleksei Novikov"
            email "novikov.aleksei.92@gmail.com"
        }
    }

    scm {
        url "https://github.com/alekseinovikov/KTypeParser"
    }
}


publishing {
    publications {
        mavenPublication(MavenPublication) {
            groupId = 'org.ktypeparser'
            artifactId = 'ktype-parser'

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'KTypeParser parses MIME types')
                root.appendNode('name', 'KTypeParser')
                root.appendNode('url', 'https://github.com/alekseinovikov/KTypeParser')
                root.children().last() + pomConfig
            }

            from components.java
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER") ?: System.properties['bintrayUser']
    key = System.getenv("BINTRAY_KEY") ?: System.properties['bintrayKey']
    configurations = ['archives']
    publications = ['mavenPublication']

    pkg {
        repo = "KTypeParser"
        name = project.name
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/alekseinovikov/KTypeParser.git'
    }
}